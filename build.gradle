buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "jitpack.io"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.4'
    }
}
apply plugin: 'forge'
apply plugin: 'signing'

version = "1.7.10-1.0.9"
group= "pers.gwyog.gtneioreplugin"
archivesBaseName = "gtneioreplugin"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    replaceIn "GTNEIOrePlugin.java"
    replace "@version@", project.version
    runDir = "eclipse"
}

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

repositories {
    mavenCentral()
	 maven {
        name = "ic2"
        url = "https://maven.ic2.player.to/"
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
}

dependencies {
    implementation "com.github.GTNewHorizons:CodeChickenLib:master-SNAPSHOT:dev"
    implementation "com.github.GTNewHorizons:CodeChickenCore:master-SNAPSHOT:dev"
    implementation "com.github.GTNewHorizons:NotEnoughItems:master-SNAPSHOT:dev"
    implementation("com.github.GTNewHorizons:GT5-Unofficial:master-SNAPSHOT:dev") {
        setChanging(true)
        transitive = false
    }
    implementation "net.industrial-craft:industrialcraft-2:2.2.828-experimental:dev"
    implementation 'com.opencsv:opencsv:4.0' //5.5.2 is a breaking update
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task signJar(dependsOn: 'reobf'){
    doLast {
        ant.signjar(
            destDir: jar.destinationDir,
            jar: jar.getArchivePath(),
            alias: findProperty('keyStoreAlias') ?: '',
            keystore: findProperty('keyStore') ?: '',
            storepass: findProperty('keyStorePass') ?: '',
            digestalg: findProperty('signDigestAlg') ?: '',
            tsaurl: findProperty('signTSAurl') ?: '',
            verbose: true
            )
    }
}